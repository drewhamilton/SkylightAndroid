apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

ext {
    artifactName = POM_ARTIFACT_ID
    publishedDescription = 'Conveniences for using Skylight on Android.'
}
apply from: '../publish.gradle'

android {
    compileSdkVersion projectVersions.compileSdk

    defaultConfig {
        minSdkVersion projectVersions.minSdk
        targetSdkVersion projectVersions.targetSdk
        versionName projectVersions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias keyAlias
            keyPassword keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    buildFeatures {
        buildConfig false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn']
    }
    lintOptions {
        lintConfig file('../lint.xml')
    }
}

dependencies {
    api(libs.androidx.appCompat)
    api(libs.androidx.lifecycle.java8)
    implementation(libs.androidx.lifecycle.runtime)

    boolean usingSkylightSnapshots = libs.versions.skylight.get().contains('SNAPSHOT')
    api(libs.skylight.core) {
        changing = usingSkylightSnapshots
    }
    implementation(libs.skylight.calculator) {
        changing = usingSkylightSnapshots
    }
    api(libs.skylight.fake) {
        changing = usingSkylightSnapshots
    }

    coreLibraryDesugaring(libs.android.coreLibraryDesugaring)

    testImplementation(libs.junit)
    testImplementation(libs.mockito)

    androidTestImplementation(libs.kotlinx.coroutinesTest)
    androidTestImplementation(libs.androidx.espresso)
    androidTestImplementation(libs.androidx.junitExt)
    androidTestImplementation(libs.materialComponents)
    androidTestImplementation(libs.truth)
}
