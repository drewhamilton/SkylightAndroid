apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

ext {
    artifact = 'skylight-factory'
    bintrayName = 'SkylightAndroid-factory'
    libraryName = 'Skylight Android Factory'
    libraryDescription = 'A simple factory for creating Skylight instances on Android.'

    compatibility = [
            ignoreMissingClasses: true,
            excludedModules: []
    ]
}
apply from: '../publish.gradle'

android {
    compileSdkVersion projectVersions.targetSdk

    defaultConfig {
        minSdkVersion projectVersions.minSdk
        targetSdkVersion projectVersions.targetSdk
        versionName projectVersions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        // Support core library desugaring:
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias keyAlias
            keyPassword keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    testOptions {
        // Needed for Process pid when checking permissions:
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        lintConfig file('../lint.xml')
    }
}

dependencies {
    implementation deps.kotlinStdLib

    api(deps.skylight) {
        changing = versions.skylight.contains('SNAPSHOT')
    }
    implementation(deps.skylightCalculator) {
        changing = versions.skylight.contains('SNAPSHOT')
    }
    implementation(deps.skylightFake) {
        changing = versions.skylight.contains('SNAPSHOT')
    }

    implementation deps.coreKtx

    coreLibraryDesugaring deps.coreLibraryDesugaring

    testImplementation deps.junit
    testImplementation deps.mockito

    androidTestImplementation deps.junitExt
    androidTestImplementation deps.espresso
}
