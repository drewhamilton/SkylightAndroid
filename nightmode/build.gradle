apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.artifact = 'skylight-nightmode'
apply from: 'publish.gradle'

android {
    compileSdkVersion targetSdk

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias keyAlias
            keyPassword keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    lintOptions {
        lintConfig file('../lint.xml')
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    api 'androidx.lifecycle:lifecycle-common-java8:2.0.0'

    api "drewhamilton.skylight.backport:skylight-backport:$skylightVersion"
    implementation "drewhamilton.skylight.backport:skylight-backport-calculator:$skylightVersion"
    implementation "drewhamilton.skylight.backport:skylight-backport-dummy:$skylightVersion"
    implementation project(':factory')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-inline:2.27.0'
    testImplementation 'org.threeten:threetenbp:1.4.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation project(':test')
    androidTestImplementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    androidTestImplementation 'com.google.android.material:material:1.0.0'
}

// TODO WORKAROUND: Get this to work in its own file
//region Compatibility
configurations {
    baseline
    latest
}

dependencies {
    baseline("$publishedGroupId:$artifact:$compatibleVersion") {
        transitive false
        force true
        exclude module: 'factory'
    }
    latest(project(path: ":$project.name", configuration: 'releaseRuntimeElements')) {
        exclude module: 'factory'
    }
}

apply plugin: 'me.champeau.gradle.japicmp'
//noinspection UnnecessaryQualifiedReference
task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask) {
    oldClasspath = configurations.baseline.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    newClasspath = configurations.latest.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    onlyBinaryIncompatibleModified true
    failOnModification failIfIncompatible
    txtOutputFile = file("$buildDir/reports/japi.txt")

    // TODO WORKAROUND: JApiCmp can't find Android classes
    ignoreMissingClasses true
}

check.dependsOn(japicmp)
build.dependsOn(check)
//endregion
