import me.champeau.gradle.japicmp.JapicmpTask

buildscript {
    dependencies {
        classpath "me.champeau.gradle:japicmp-gradle-plugin:$versions.jApiCmp"
    }
    repositories {
        gradlePluginPortal()
    }
}

def compatibleVersion = '0.11.0'
def failIfIncompatible = false
subprojects { subproject ->
    afterEvaluate {
        // TODO: Stop skipping skylight-themes once it's published
        if (!subproject.hasProperty('artifact') || subproject.artifact == 'skylight-themes')
            return

        configurations {
            baseline
            latest
        }

        dependencies {
            baseline("$publishedGroupId:$project.artifact:$compatibleVersion") {
                transitive false
                force true
                for (module in subproject.ext.compatibility.excludedModules)
                    exclude module: module
            }
            latest(project(path: ":$project.name", configuration: 'releaseRuntimeElements')) {
                for (module in subproject.ext.compatibility.excludedModules)
                    exclude module: module
            }
        }

        subproject.apply plugin: 'me.champeau.gradle.japicmp'
        task japicmp(type: JapicmpTask) {
            oldClasspath = configurations.baseline.incoming.artifactView { config ->
                config.attributes { container ->
                    container.attribute(Attribute.of("artifactType", String.class), "jar")
                }
            }.artifacts.artifactFiles

            newClasspath = configurations.latest.incoming.artifactView { config ->
                config.attributes { container ->
                    container.attribute(Attribute.of("artifactType", String.class), "jar")
                }
            }.artifacts.artifactFiles

            onlyBinaryIncompatibleModified true
            failOnModification failIfIncompatible
            txtOutputFile = file("$buildDir/reports/japi.txt")

            ignoreMissingClasses subproject.ext.compatibility.ignoreMissingClasses
        }

        check.dependsOn(japicmp)
        build.dependsOn(check)
    }
}
