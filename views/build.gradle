apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.artifact = 'skylight-views'
apply from: 'publish.gradle'

android {
    compileSdkVersion targetSdk

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias keyAlias
            keyPassword keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // TODO: Should this be api or compileOnly? If api, should it be ThreeTenABP instead?
    api 'org.threeten:threetenbp:1.4.0:no-tzdb'

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'

    implementation 'com.google.android.material:material:1.1.0-alpha07'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-inline:2.28.2'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation project(':test')
    androidTestImplementation 'drewhamilton.inlinedimens:inlinedimens:0.4.0'
}

// TODO WORKAROUND: Get this to work in its own file
//region Compatibility
configurations {
    baseline
    latest
}

dependencies {
    baseline("$publishedGroupId:$artifact:$compatibleVersion") {
        transitive false
        force true
    }
    latest project(path: ":$project.name", configuration: 'releaseRuntimeElements')
}

apply plugin: 'me.champeau.gradle.japicmp'
//noinspection UnnecessaryQualifiedReference
task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask) {
    oldClasspath = configurations.baseline.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    newClasspath = configurations.latest.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    onlyBinaryIncompatibleModified true
    failOnModification failIfIncompatible
    txtOutputFile = file("$buildDir/reports/japi.txt")

    // TODO WORKAROUND: JApiCmp can't find Android classes
    ignoreMissingClasses true
}

check.dependsOn(japicmp)
build.dependsOn(check)
//endregion
