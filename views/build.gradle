apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext {
    artifact = 'skylight-views'
    bintrayName = 'SkylightAndroid-views'
    libraryName = 'Skylight Views'
    libraryDescription = 'Custom Android views that useful in displaying Skylight information.'
}

android {
    compileSdkVersion projectVersions.targetSdk

    defaultConfig {
        minSdkVersion projectVersions.minSdk
        targetSdkVersion projectVersions.targetSdk
        versionName projectVersions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias keyAlias
            keyPassword keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    // Generate R when mocking (material) views:
    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    implementation deps.kotlinStdLib

    // TODO: Should this be api or compileOnly? If api, should it be ThreeTenABP instead?
    api deps.threeTenBpNoTzDb

    implementation deps.appCompat
    implementation deps.constraintLayout

    implementation deps.materialComponents

    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.mockitoKotlin

    androidTestImplementation deps.testRunner
    androidTestImplementation deps.testRules
    androidTestImplementation deps.espresso
    androidTestImplementation project(':test')
    androidTestImplementation deps.inlineDimens
}

// TODO WORKAROUND: Get this to work in its own file
//region Compatibility
configurations {
    baseline
    latest
}

dependencies {
    baseline("$group:$artifact:$compatibleVersion") {
        transitive false
        force true
    }
    latest project(path: ":$project.name", configuration: 'releaseRuntimeElements')
}

apply plugin: 'me.champeau.gradle.japicmp'
//noinspection UnnecessaryQualifiedReference
task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask) {
    oldClasspath = configurations.baseline.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    newClasspath = configurations.latest.incoming.artifactView { config ->
        config.attributes { container ->
            container.attribute(Attribute.of("artifactType", String.class), "jar")
        }
    }.artifacts.artifactFiles

    onlyBinaryIncompatibleModified true
    failOnModification failIfIncompatible
    txtOutputFile = file("$buildDir/reports/japi.txt")

    // TODO WORKAROUND: JApiCmp can't find Android classes
    ignoreMissingClasses true
}

check.dependsOn(japicmp)
build.dependsOn(check)
//endregion
